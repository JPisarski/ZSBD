-- zadanie 1

CREATE OR REPLACE PACKAGE paczka_zadania_6_i_7 IS

    PROCEDURE add_job(p_job_id IN jobs.job_id%TYPE, p_job_title IN jobs.job_title%TYPE);

    PROCEDURE update_job_title(p_job_id IN jobs.job_id%TYPE, p_new_title IN jobs.job_title%TYPE);

    PROCEDURE delete_job(p_job_id IN jobs.job_id%TYPE);

    PROCEDURE get_employer_data(p_id IN employees.employee_id%TYPE, 
                                p_salary OUT employees.salary%TYPE, 
                                p_last_name OUT employees.last_name%TYPE);

    PROCEDURE add_employee(p_first_name IN employees.first_name%TYPE DEFAULT NULL,
                           p_last_name IN employees.last_name%TYPE DEFAULT NULL,
                           p_salary IN employees.salary%TYPE DEFAULT 10000,
                           p_job_id IN employees.job_id%TYPE DEFAULT 'IT_PROG',
                           p_manager_id IN employees.manager_id%TYPE DEFAULT NULL,
                           p_dept_id IN employees.department_id%TYPE DEFAULT 60,
                           p_email IN employees.email%TYPE DEFAULT 'pracownik@wp.pl',
                           p_hire_date IN employees.hire_date%TYPE DEFAULT '01/01/26');

    PROCEDURE avg_salary_by_manager(p_manager_id IN employees.manager_id%TYPE, p_avg_salary OUT NUMBER);

    PROCEDURE raise_department_salary(p_department_id IN departments.department_id%TYPE,
                                      p_procent IN NUMBER);

    PROCEDURE move_employee(p_emp_id IN employees.employee_id%TYPE,
                            p_new_dept_id IN departments.department_id%TYPE);

    PROCEDURE delete_department(p_dept_id IN departments.department_id%TYPE);

    FUNCTION get_job_title(p_job_id IN jobs.job_id%TYPE) RETURN VARCHAR2;
    
    FUNCTION yearly_salary(p_emp_id IN employees.employee_id%TYPE) RETURN NUMBER;
    
    FUNCTION get_area_code(p_phone IN VARCHAR2) RETURN VARCHAR2;
    
    FUNCTION capitalize_edges(p_text IN VARCHAR2) RETURN VARCHAR2;
    
    FUNCTION pesel_to_date(p_pesel IN VARCHAR2) RETURN DATE;
    
    FUNCTION stats_by_country(p_country_name IN countries.country_name%TYPE) RETURN VARCHAR2;
    
    FUNCTION gen_access_id(p_last_name IN VARCHAR2, p_phone IN VARCHAR2, p_first_name IN VARCHAR2) RETURN VARCHAR2;
    
END paczka_zadania_6_i_7;
/

CREATE OR REPLACE PACKAGE BODY paczka_zadania_6_i_7 IS

    PROCEDURE add_job(p_job_id IN jobs.job_id%TYPE, p_job_title IN jobs.job_title%TYPE) IS
    BEGIN
        INSERT INTO jobs(job_id, job_title) VALUES(p_job_id, p_job_title);
        DBMS_OUTPUT.PUT_LINE('Dodano do tabeli JOBS pracę o id: ' || p_job_id);
    EXCEPTION
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Błąd dodawania do tabeli JOBS: ' || SQLERRM);
    END add_job;

    PROCEDURE update_job_title(p_job_id IN jobs.job_id%TYPE, p_new_title IN jobs.job_title%TYPE) IS
        e_no_jobs_updated EXCEPTION;
        PRAGMA EXCEPTION_INIT(e_no_jobs_updated, -20005);
    BEGIN
        UPDATE jobs SET job_title = p_new_title WHERE job_id = p_job_id;
        IF SQL%ROWCOUNT = 0 THEN
            RAISE e_no_jobs_updated;
        END IF;
        DBMS_OUTPUT.PUT_LINE('Zaktualizowano w tabeli JOBS pracę o id: ' || p_job_id);
    EXCEPTION
        WHEN e_no_jobs_updated THEN
            DBMS_OUTPUT.PUT_LINE('Nieprawidłowe id pracy!');
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Błąd modyfikacji w tabeli JOBS: ' || SQLERRM);
    END update_job_title;

    PROCEDURE delete_job(p_job_id IN jobs.job_id%TYPE) IS
        e_no_jobs_deleted EXCEPTION;
    BEGIN
        DELETE FROM jobs WHERE job_id = p_job_id;
        IF SQL%ROWCOUNT = 0 THEN
            RAISE e_no_jobs_deleted;
        END IF;
        DBMS_OUTPUT.PUT_LINE('Usunięto z tabeli JOBS pracę o id: ' || p_job_id);
    EXCEPTION
        WHEN e_no_jobs_deleted THEN
            DBMS_OUTPUT.PUT_LINE('Nieprawidłowe id pracy!');
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Błąd usuwania pracy: ' || SQLERRM);
    END delete_job;

    PROCEDURE get_employer_data(p_id IN employees.employee_id%TYPE, 
                                p_salary OUT employees.salary%TYPE, 
                                p_last_name OUT employees.last_name%TYPE) IS
    BEGIN
        SELECT salary, last_name INTO p_salary, p_last_name 
        FROM employees WHERE employee_id = p_id;
    EXCEPTION
        WHEN NO_DATA_FOUND THEN
            DBMS_OUTPUT.PUT_LINE('Nie znaleziono pracownika o danym id');
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Błąd: ' || SQLERRM);
    END get_employer_data;

    PROCEDURE add_employee(p_first_name IN employees.first_name%TYPE DEFAULT NULL,
                           p_last_name IN employees.last_name%TYPE DEFAULT NULL,
                           p_salary IN employees.salary%TYPE DEFAULT 10000,
                           p_job_id IN employees.job_id%TYPE DEFAULT 'IT_PROG',
                           p_manager_id IN employees.manager_id%TYPE DEFAULT NULL,
                           p_dept_id IN employees.department_id%TYPE DEFAULT 60,
                           p_email IN employees.email%TYPE DEFAULT 'pracownik@wp.pl',
                           p_hire_date IN employees.hire_date%TYPE DEFAULT '01/01/26') IS
        e_salary_too_high EXCEPTION;
    BEGIN
        IF p_salary > 20000 THEN
            RAISE e_salary_too_high;
        END IF;
        INSERT INTO employees(employee_id, first_name, last_name, job_id, salary, manager_id, department_id, email, hire_date)
        VALUES (employee_seq.NEXTVAL, p_first_name, p_last_name, p_job_id, p_salary, p_manager_id, p_dept_id, p_email, p_hire_date);
        DBMS_OUTPUT.PUT_LINE('Dodano pracownika o imieniu i nazwisku: ' || p_first_name || ' ' || p_last_name);
    EXCEPTION
        WHEN e_salary_too_high THEN
            DBMS_OUTPUT.PUT_LINE('Wynagrodzenie przekracza 20000!');
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Błąd: ' || SQLERRM);
    END add_employee;

    PROCEDURE avg_salary_by_manager(p_manager_id IN employees.manager_id%TYPE, p_avg_salary OUT NUMBER) IS
    BEGIN
        SELECT AVG(salary) INTO p_avg_salary FROM employees WHERE manager_id = p_manager_id;
        IF p_avg_salary IS NULL THEN
            DBMS_OUTPUT.PUT_LINE('Manager o id ' || p_manager_id || ' nie ma podwładnych.');
        END IF;
    EXCEPTION
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Błąd: ' || SQLERRM);
    END avg_salary_by_manager;

    PROCEDURE raise_department_salary(p_department_id IN departments.department_id%TYPE, p_procent IN NUMBER) IS
        v_count NUMBER := 0;
        e_department_not_found EXCEPTION;
    BEGIN
        FOR emp IN (
            SELECT e.employee_id, e.salary, e.job_id, j.min_salary, j.max_salary 
            FROM employees e JOIN jobs j ON e.job_id = j.job_id 
            WHERE e.department_id = p_department_id
        )
        LOOP
            DECLARE
                v_new_salary NUMBER;
            BEGIN
                v_new_salary := emp.salary * (1 + p_procent/100);
                IF v_new_salary < emp.min_salary THEN
                    v_new_salary := emp.min_salary;
                ELSIF v_new_salary > emp.max_salary THEN
                    v_new_salary := emp.max_salary;
                END IF;
                UPDATE employees
                SET salary = v_new_salary
                WHERE employee_id = emp.employee_id;
                v_count := v_count + 1;
            END;
        END LOOP;
        IF v_count = 0 THEN
            RAISE e_department_not_found;
        END IF;
        DBMS_OUTPUT.PUT_LINE('Zwiększono wynagrodzenia danej liczbie pracowników: ' || v_count);
    EXCEPTION
        WHEN e_department_not_found THEN
            DBMS_OUTPUT.PUT_LINE('Nieprawidłowe id departamentu!');
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Inny błąd: ' || SQLERRM);
    END raise_department_salary;

    PROCEDURE move_employee(p_emp_id IN employees.employee_id%TYPE, p_new_dept_id IN departments.department_id%TYPE) IS
        v_temp NUMBER;
        e_no_employee EXCEPTION;
        e_no_department EXCEPTION;
    BEGIN
        SELECT COUNT(*) INTO v_temp FROM employees WHERE employee_id = p_emp_id;
        IF v_temp = 0 THEN
            RAISE e_no_employee;
        END IF;

        SELECT COUNT(*) INTO v_temp FROM departments WHERE department_id = p_new_dept_id;
        IF v_temp = 0 THEN
            RAISE e_no_department;
        END IF;

        UPDATE employees SET department_id = p_new_dept_id WHERE employee_id = p_emp_id;
        DBMS_OUTPUT.PUT_LINE('Przeniesiono pracownika o id: ' || p_emp_id || ' do departamentu o id: ' || p_new_dept_id);
    EXCEPTION
        WHEN e_no_employee THEN
            DBMS_OUTPUT.PUT_LINE('Nieprawidłowe id pracownika!');
        WHEN e_no_department THEN
            DBMS_OUTPUT.PUT_LINE('Nieprawidłowe id departamentu!');
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Błąd podczas przenoszenia: ' || SQLERRM);
    END move_employee;

    PROCEDURE delete_department(p_dept_id IN departments.department_id%TYPE) IS
        v_count NUMBER := 0;
    BEGIN
        SELECT COUNT(*) INTO v_count FROM employees WHERE department_id = p_dept_id;
        IF v_count > 0 THEN
            DBMS_OUTPUT.PUT_LINE('Nie można usunąć, bo są pracownicy w departamencie.');
            RETURN;
        END IF;
        DELETE FROM departments WHERE department_id = p_dept_id;
        IF SQL%ROWCOUNT = 0 THEN
            DBMS_OUTPUT.PUT_LINE('Nieprawidłowe id departamentu!');
            RETURN;
        END IF;
        DBMS_OUTPUT.PUT_LINE('Usunięto departament o id: ' || p_dept_id);
    EXCEPTION
        WHEN OTHERS THEN
            DBMS_OUTPUT.PUT_LINE('Błąd usuwania departamentu: ' || SQLERRM);
    END delete_department;

    FUNCTION get_job_title(p_job_id IN jobs.job_id%TYPE) RETURN VARCHAR2 IS
        v_title jobs.job_title%TYPE;
    BEGIN
        SELECT job_title INTO v_title FROM jobs WHERE job_id = p_job_id;
        RETURN v_title;
    EXCEPTION
        WHEN NO_DATA_FOUND THEN
            RETURN 'Taka praca nie istnieje!';
    END get_job_title;

    FUNCTION yearly_salary(p_emp_id IN employees.employee_id%TYPE) RETURN NUMBER IS
        v_sal employees.salary%TYPE;
        v_comm employees.commission_pct%TYPE;
    BEGIN
        SELECT salary, NVL(commission_pct, 0) INTO v_sal, v_comm FROM employees WHERE employee_id = p_emp_id;
        RETURN v_sal * 12 + (v_sal * v_comm);
    EXCEPTION
        WHEN NO_DATA_FOUND THEN
            RETURN NULL;
    END yearly_salary;

    FUNCTION get_area_code(p_phone IN VARCHAR2) RETURN VARCHAR2 IS
    BEGIN
        RETURN '(' || SUBSTR(p_phone, 1, INSTR(p_phone, '.') - 1) || ')' || 
               SUBSTR(REPLACE(p_phone, '.', ''), INSTR(p_phone, '.'));
    END get_area_code;

    FUNCTION capitalize_edges(p_text IN VARCHAR2) RETURN VARCHAR2 IS
    BEGIN
        IF p_text IS NULL OR LENGTH(p_text) < 2 THEN
            RETURN p_text;
        END IF;
        RETURN UPPER(SUBSTR(p_text, 1, 1))
               || LOWER(SUBSTR(p_text, 2, LENGTH(p_text) - 2))
               || UPPER(SUBSTR(p_text, -1, 1));
    END capitalize_edges;

    FUNCTION pesel_to_date(p_pesel IN VARCHAR2) RETURN DATE IS
        v_year  NUMBER;
        v_month NUMBER;
        v_day   NUMBER;
    BEGIN
        v_year  := SUBSTR(p_pesel, 1, 2);
        v_month := SUBSTR(p_pesel, 3, 2);
        v_day   := SUBSTR(p_pesel, 5, 2);
        IF v_month BETWEEN 1 AND 12 THEN
            v_year := 1900 + v_year;
        ELSIF v_month BETWEEN 21 AND 32 THEN
            v_year := 2000 + v_year;
            v_month := v_month - 20;
        END IF;
        RETURN TO_DATE(v_year || '-' || v_month || '-' || v_day, 'YYYY-MM-DD');
    EXCEPTION
        WHEN OTHERS THEN
            RETURN NULL;
    END pesel_to_date;

    FUNCTION stats_by_country(p_country_name IN countries.country_name%TYPE) RETURN VARCHAR2 IS
        v_country_id countries.country_id%TYPE;
        v_emp_count NUMBER;
        v_dept_count NUMBER;
    BEGIN
        SELECT country_id INTO v_country_id FROM countries WHERE country_name = p_country_name;
        SELECT COUNT(*) INTO v_dept_count FROM departments d
        JOIN locations l ON d.location_id = l.location_id
        WHERE l.country_id = v_country_id;
        SELECT COUNT(*) INTO v_emp_count FROM employees e
        JOIN departments d ON e.department_id = d.department_id
        JOIN locations l ON d.location_id = l.location_id
        WHERE l.country_id = v_country_id;
        RETURN 'Pracownicy: ' || v_emp_count || ', Departamenty: ' || v_dept_count;
    EXCEPTION
        WHEN NO_DATA_FOUND THEN
            RETURN 'Brak takiego kraju!';
    END stats_by_country;

    FUNCTION gen_access_id(p_last_name IN VARCHAR2, p_phone IN VARCHAR2, p_first_name IN VARCHAR2) RETURN VARCHAR2 IS
        v_last4 VARCHAR2(4);
    BEGIN
        v_last4 := SUBSTR(REPLACE(p_phone, '.', ''), -4);
        RETURN UPPER(SUBSTR(p_last_name, 1, 3)) || v_last4 || UPPER(SUBSTR(p_first_name, 1, 1));
    END gen_access_id;
    
END paczka_zadania_6_i_7;
/

BEGIN
    paczka_zadania_6_i_7.add_job('IT_TEST', 'TESTER');
    DBMS_OUTPUT.PUT_LINE('IT_PROG to id pracy o nazwie: ' || paczka_zadania_6_i_7.get_job_title('IT_PROG'));
END;
/

-- zadanie 2

CREATE OR REPLACE PACKAGE paczka_regiony AS

    PROCEDURE add_region(p_region_id NUMBER, p_region_name VARCHAR2);
    PROCEDURE update_region(p_region_id NUMBER, p_region_name VARCHAR2);
    PROCEDURE delete_region(p_region_id NUMBER);

    FUNCTION get_region_name(p_region_id NUMBER) RETURN VARCHAR2;
    FUNCTION find_id_region_by_name(p_name VARCHAR2) RETURN NUMBER;
    
END paczka_regiony;
/

CREATE OR REPLACE PACKAGE BODY paczka_regiony AS

    PROCEDURE add_region(p_region_id NUMBER, p_region_name VARCHAR2) IS
    BEGIN
        INSERT INTO regions(region_id, region_name) VALUES(p_region_id, p_region_name);
    END;

    PROCEDURE update_region(p_region_id NUMBER, p_region_name VARCHAR2) IS
    BEGIN
        UPDATE regions SET region_name = p_region_name WHERE region_id = p_region_id;
    END;

    PROCEDURE delete_region(p_region_id NUMBER) IS
    BEGIN
        DELETE FROM regions WHERE region_id = p_region_id;
    END;

    FUNCTION get_region_name(p_region_id NUMBER)
    RETURN VARCHAR2 IS
        v_name VARCHAR2(50);
    BEGIN
        SELECT region_name INTO v_name
        FROM regions WHERE region_id = p_region_id;
        RETURN v_name;
    END;

    FUNCTION find_id_region_by_name(p_name VARCHAR2)
    RETURN NUMBER IS
        v_id NUMBER;
    BEGIN
        SELECT region_id INTO v_id
        FROM regions WHERE region_name = p_name;
        RETURN v_id;
    END;
    
END paczka_regiony;
/

BEGIN
    paczka_regiony.add_region(10, 'Grenlandia');
    DBMS_OUTPUT.PUT_LINE('10 to id regionu o nazwie: ' || paczka_regiony.get_region_name(10));
END;
/


-- zadanie 3

CREATE TABLE tabela_audytowa (
    id NUMBER PRIMARY KEY,
    err_msg VARCHAR2(4000),
    err_date DATE,
    username VARCHAR2(50)
);

CREATE SEQUENCE audyt_seq START WITH 1;

CREATE OR REPLACE PACKAGE paczka_regiony_dodatek AS

    e_duplicate_region EXCEPTION;
    e_region_with_countries EXCEPTION;

    PROCEDURE log_error(p_message VARCHAR2);
    PROCEDURE add_region(p_region_id NUMBER, p_region_name VARCHAR2);
    PROCEDURE delete_region(p_region_id NUMBER);
    
END paczka_regiony_dodatek;
/

CREATE OR REPLACE PACKAGE BODY paczka_regiony_dodatek AS

    PROCEDURE log_error(p_message VARCHAR2) IS
    BEGIN
        INSERT INTO tabela_audytowa(id, err_msg, err_date, username)
        VALUES(audyt_seq.NEXTVAL, p_message, SYSDATE, USER);
    END;

    PROCEDURE add_region(p_region_id NUMBER, p_region_name VARCHAR2) IS
        v_count NUMBER;
    BEGIN
        SELECT COUNT(*) INTO v_count
        FROM regions WHERE region_name = p_region_name;

        IF v_count > 0 THEN
            
            RAISE e_duplicate_region;
        END IF;

        INSERT INTO regions VALUES(p_region_id, p_region_name);
        DBMS_OUTPUT.PUT_LINE('Dodano region o id: ' || p_region_id || ' i nazwie: ' || p_region_name);
        
        EXCEPTION
            WHEN e_duplicate_region THEN
                log_error('Próba dodania regionu o istniejącej nazwie: ' || p_region_name);
                DBMS_OUTPUT.PUT_LINE('Błąd: Region o nazwie ' || p_region_name || ' już istnieje.');
            WHEN OTHERS THEN
                log_error('Błąd w add_region: ' || SQLERRM);
                DBMS_OUTPUT.PUT_LINE('Wystąpił nieoczekiwany błąd w add_region: ' || SQLERRM);
    END;

    PROCEDURE delete_region(p_region_id NUMBER) IS
        v_count NUMBER;
    BEGIN
        SELECT COUNT(*) INTO v_count
        FROM countries WHERE region_id = p_region_id;

        IF v_count > 0 THEN
            RAISE e_region_with_countries;
        END IF;

        DELETE FROM regions WHERE region_id = p_region_id;
        DBMS_OUTPUT.PUT_LINE('Usunięto region o id: ' || p_region_id);
        
        EXCEPTION
            WHEN e_region_with_countries THEN
                log_error('Próba usunięcia regionu z przypisanymi krajami: ' || p_region_id);
                DBMS_OUTPUT.PUT_LINE('Błąd: Region o id ' || p_region_id || ' jest przypisany do krajów i nie może zostać usunięty.');
            WHEN OTHERS THEN
                log_error('Błąd w delete_region: ' || SQLERRM);
                DBMS_OUTPUT.PUT_LINE('Wystąpił nieoczekiwany błąd w delete_region: ' || SQLERRM);
    END;


END paczka_regiony_dodatek;
/

BEGIN
    paczka_regiony_dodatek.add_region(5, 'Grenlandia');
    paczka_regiony_dodatek.delete_region(1);
END;
/

-- zadanie 4

CREATE OR REPLACE PACKAGE paczka_departament_statystyka AS
    FUNCTION avg_salary(p_dept_id NUMBER) RETURN NUMBER;
    FUNCTION job_minmax(p_job_id VARCHAR2) RETURN VARCHAR2;
    PROCEDURE gen_report(p_dept_id NUMBER);
END paczka_departament_statystyka;
/

CREATE OR REPLACE PACKAGE BODY paczka_departament_statystyka AS

    FUNCTION avg_salary(p_dept_id NUMBER)
    RETURN NUMBER IS
        v_avg NUMBER;
    BEGIN
        SELECT AVG(salary) INTO v_avg
        FROM employees WHERE department_id = p_dept_id;
        RETURN v_avg;
    END;

    FUNCTION job_minmax(p_job_id VARCHAR2)
    RETURN VARCHAR2 IS
        v_min NUMBER;
        v_max NUMBER;
    BEGIN
        SELECT min_salary, max_salary INTO v_min, v_max
        FROM jobs WHERE job_id = p_job_id;

        RETURN 'MIN=' || v_min || ' MAX=' || v_max;
    END;

    PROCEDURE gen_report(p_dept_id IN NUMBER) IS
        v_avg NUMBER;
        v_count NUMBER;
    BEGIN
        SELECT AVG(salary), COUNT(*) INTO v_avg, v_count
        FROM employees WHERE department_id = p_dept_id;

        DBMS_OUTPUT.PUT_LINE('RAPORT Departmanet o id: ' || p_dept_id);
        DBMS_OUTPUT.PUT_LINE('Liczba pracowników: ' || v_count);
        DBMS_OUTPUT.PUT_LINE('Średnie wynagrodzenie: ' || v_avg);
    END;

END paczka_departament_statystyka;
/

BEGIN
    DBMS_OUTPUT.PUT_LINE('Średnie wynagrodzenie: ' || paczka_departament_statystyka.avg_salary(60));
    DBMS_OUTPUT.PUT_LINE('Stanowisko Programmer: ' ||paczka_departament_statystyka.job_minmax('IT_PROG'));
    paczka_departament_statystyka.gen_report(100);    
END;
/

-- zadanie 5

CREATE OR REPLACE PACKAGE paczka_dane AS
    PROCEDURE validate_phones;
    PROCEDURE mass_raise(p_job_id VARCHAR2, p_percent NUMBER);
END paczka_dane;
/

CREATE OR REPLACE PACKAGE BODY paczka_dane AS

    PROCEDURE validate_phones IS
    BEGIN
        UPDATE employees
        SET phone_number = 
            SUBSTR(REGEXP_REPLACE(phone_number, '[^0-9]', ''), 1, 3) || '-' ||
            SUBSTR(REGEXP_REPLACE(phone_number, '[^0-9]', ''), 4, 3) || '-' ||
            SUBSTR(REGEXP_REPLACE(phone_number, '[^0-9]', ''), 7);
        DBMS_OUTPUT.PUT_LINE('Automatyczna korekta formatu numerów telefonów.');
    END;

    PROCEDURE mass_raise(p_job_id VARCHAR2, p_percent NUMBER) IS
    BEGIN
        UPDATE employees
        SET salary = salary * (1 + p_percent/100)
        WHERE job_id = p_job_id;
        DBMS_OUTPUT.PUT_LINE('Masowa aktualizacja pensji dla stanowisk z określonym procentem podwyżki');
    END;
END paczka_dane;
/

BEGIN
    paczka_dane.validate_phones();
    paczka_dane.mass_raise('IT_PROG', 10);
END;
/
